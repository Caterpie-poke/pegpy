/* すもも文法 */

/* Indent-based Code Layout */

EOF          = !.
NL           = '\n' / EOF
S            = [ \t\r　、，]

_            = ( S / BLOCKCOMMENT / LINECOMMENT )*
__           = ( S / '\n' / BLOCKCOMMENT / LINECOMMENT )*
SPC          = ( S / BLOCKCOMMENT / LINECOMMENT )+

BLOCKCOMMENT = '/*' ( !'*/' . )* '*/'
             / '(*' ( !'*)' . )* '*)'
LINECOMMENT  = '//' ( !NL . )*

EOL          = _ NL (_ NL)*
INDENT       = '\n' [ \t]+

/* Character */

C  =
    / KANJI
    / KATA
    / [ａ-ｚＡ-Ｚ０-９]

HIRA = [ぁ-ん]
KATA = [ァ-ヶー・]
KANJI = [㐀-䶵一-龠々〇〻ーご]
MARK = [ー]
TEN  = [、，, ]

CC = C+
example CC 愛、夢
example CC ホーム

/* 定義 */

example Sentence スモモもモモもモモです。
example Sentence 「すもも」も「もも」も「もも」です。

Sentence = {
    (Argument / Connective / Connector)*
    (Declinable &EOS / NounPhrase / Error?)
    #Sentence
} EOS

Error = !EOS {
    (!EOS .)+
    #err
}

EOS = [。．.\n] / EOF

// N 格助詞
// 連体

// V 副助詞 V
VV = 'ので' / 'から'
   / 'ても' / 'ところで' / 'のに' / 'ものの' /'ながら' / 'けれど'
   / 'ながら' / 'たり' / [ばとてでがし]

// 接続詞 フォーマルなもの

THEN = 'だから' / 'そのため' / 'このため' / 'それで' / 'そこで' / 'よって' / 'すると' / 'だとすると' / 'ゆえに' / 'それゆえに' / 'したがって' / 'それなら' / 'それでは' / 'ならば' / 'だとしたら' / 'そうすると' / 'そうしたら' / 'そうでないなら' / 'そうしないと' / 'さもないと'
BUT = 'しかし' / 'しかしながら' / 'ただ' / 'だが' / 'が' / 'けど' / 'けれど' / 'けれども' / 'だけど' / 'だけども' / 'でも' / 'それでも' / 'ではあるが' / 'そうではあるが' / 'ところが' / 'ですが' / 'にもかかわらず' / 'ものの' / 'のに' / 'なのに' / 'それなのに' / 'しかるに' / 'とはいえ' / 'とはいうものの' / 'そうはいうものの' / 'そのくせ'
AFTERTHAT= 'および' / 'かつ' / 'それから' / 'ならびに' / '同じく' / 'また'
MOREOVER='さらに' / 'また' / 'しかも' / 'おまけに' / 'そのうえ' / 'そして' / 'それから' / 'それに' / 'それにしても' / 'そればかりでなく' / 'そればかりか' / 'それどころか' / 'どころか' / 'それに加えて' / 'ひいては' / 'なお' / 'しかも' / 'ともあれ' / '加えて'
WHEREAS='一方' / '他方' / '逆に' / '反対に' / '対して' / 'それに対して' / '反面' / 'その反面'
FIRST= '第' _ ORD _ 'に'? / ORD _ 'つ' '目は'? / 'はじめに' / '最初に' / '続いて' / '次いで' / '最後に' / 'その後' / 'まず' / '次に'
SINCE='なぜなら' / 'なぜならば' / 'なぜかというと' / 'というのは' / 'というのも' / 'だって' / 'なにしろ' / 'なにせ'
INFACT='因みに' / 'なお' / 'ただし' / 'ただ' / 'もっとも' / 'そもそも' / 'そのかわり' / '実は' / '実のところ' / '実は言うと'
THATIS='つまり' / 'すなわち' / '要は' / '要するに' / 'いわば' / '言い換えると' / '換言すると' / '言ってみれば' / 'と言うより' / 'というか' / 'むしろ' / 'かえって' / 'そうではなく' / 'かわりに' / 'そのかわり'
FOREX='具体的には' / 'たとえば' / 'いわば' / 'いってみれば' / 'とりわけ' / '特に' / 'なかでも' / 'とりわけ' / '殊に'
BYTHEWAY='さて' / 'ところで' / 'ときに' / '次に' / 'ともあれ' / 'では' / 'それでは' / 'それはさておき' / 'それはそうと' / 'それにしても' / 'そういえば' / 'じゃあ'
FINALLY = ('以上'/'結局')? _ 'まとめると'? / 'まとめると'

ORD = [0-9０-９零一-九十百千万億]

Then = { THEN #Then }
However = { BUT #However }
Likewize = { '同じく' / '同様に' #Likewise }
Moreover = { MOREOVER #Moreover }
Whereas = { WHEREAS #Whereas }
AtFirst = { FIRST #Itemize }
Since = { SINCE #Since }
Indeed = { INFACT #Indeed }
ThatIs = { THATIS #ThatIs }
ForExample = { FOREX #ForExample }
ByTheWay = { BYTHEWAY #ByTheWay }
Fainally = { FINALLY #Finally }

Connector = {
    Then
    / However
    / Likewize
    / Moreover
    / Whereas
    / AtFirst
    / Since
    / Indeed
    / ThatIs
    / ForExample
    / ByTheWay
    / Fainally
    #Connector
} _

// 名詞

NOUN = NP / @in(NEWNOUN) / DNOUN / NS
N    = NP / @in(NEWNOUN) / DNOUN / C+
NP = '「' @def(NEWNOUN) '」'
NEWNOUN = (NP / !'」' .)+

// ひらがなを含まない名詞はここに定義する
DNOUN =
    / 'うち'
    / 'こと' / 'ここ' / 'これ'
    / 'ひとつ' / 'ひらがな'
    / 'もうひとつ'
    / '残り'

//NS = (C / !NC .)+
NS = C+
NC = NN (C / NOUN) / NV (C / TEN)

// 連体助詞
NN = 'のような' / [のとやな]
// 連用助詞
NV =
    / 'よりも' / 'より' / 'から' / 'まで' 'に'? / 'でも' / 'しか' / 'こそ' / 'さえ'
    / 'が' ('なければ' / 'なかったのなら')
    / 'により' / 'には'/ 'につき'
    / 'に関して' 'は'? / 'について' 'は'? / 'に基づいて' / 'によって' / 'にも関わらず'
    / 'にしたがって' / 'に従って' / 'に対して' / 'に反して' / 'にそって'
    / 'を使って' / 'を用いて' / 'を加えて' / 'を囲んで' / 'を' ('のぞ' / '除') 'いて' 'は'?
    / 'を通って' / 'を経由して'
    / 'の' ([上下右左中外内方前後間他] '側'? / 'うち' / '近く' / '周り' / '周辺' / 'そば' / '範囲' '内'?) [にへで] 'は'?
    / 'のために' / 'のように' / 'のいたるところで'
    / 'として' 'は'? / 'とは' / 'と違って' / 'と異なって'
    / 'と一緒に' / 'と共に'
    / 'なし' [でに]
    / 'ずっと' / 'ごと' 'に'?
    / 'では'
    / [はもがをにへでて]

Noun = {
    NOUN
    #Noun
}

LongNoun =
    / Declinable ^{ Noun #ComplexNoun }
    / AdjectivePhrase ^{ Noun #ComplexNoun }
    / Noun

NounPhrase =
    LongNoun ( ^{ { NN } _ LongNoun #Infix } )*

Suffix = {
    (NV/NN)
    #Suffix
} _

Argument = {
    AdjectivePhrase?
    NounPhrase
    Suffix
    #NounPhrase
}

// 形容詞

//ADJ = (!([いな] (C / N / EOS )) .)+ [いな]
//NADJ = (!('ない' (C / N / EOS )) .)+ 'ない'
//example ADJ とても美しい人
//example NADJ とても美しくない人
//example ADJ 素敵な人
//example N 使って攻撃する

PREFIX =
    / 'あの' 'ような'? / 'ある'
    / 'この' 'ような'?
    / 'さまざまな' / '様々な'
    / 'その' 'ような'?

DADJ =
    / [強弱良よい悪長短早速遅多甘辛安古暑寒熱広狭若暗痛眠偉赤青白黒黄紅]
    / '明る' / 'すご' / '危な'
    / '大き' / '小さ' / '少な'
    / '美し' / '暖か' / '涼し'
    / '難し' / '易し' / 'おもしろ' / 'おいし' / 'おかし' / 'かわい'
    / '忙し' / '楽し' / '新し' / '冷た' / 'ほし' / '欲し' / 'うるさ'

DADVERB =
    / 'ちょっと'
    / 'いっしょに'
    / 'いつも' / 'ずっと' / '常に'
    / 'ときどき' / '時々'
    / 'もう' '既に'? / '一度も'
    / 'まだ' / 'あまり' / 'そんなに'
    / 'とても'
    / 'そろそろ' / '全然' / '全く'
    / 'だいたい' / 'たくさん' / 'ほとんど'
    / '全部で'
    / '初めて' / 'また'
    / '少々' / '少しだけ'
    / '別々に' / 'まっすぐ'
    / 'ゆっくり' / 'なかなか'
    / 'すぐ' / 'だんだん'
    / 'ぜひ' / '是非'
    / 'たぶん'
    / 'もちろん'
    / 'ほんとうに'
    / 'いつでも' / 'どこでも' / 'だれでも' / '何でも' / 'いくら'
    / '直接'
    / 'ずいぶん'
    / 'はっきり'
    / 'いつか'
    / '自由に' / '絶対に' / '上手に' / '急に' / '無理に'
    / 'しばらく'
    / 'たいてい' / 'ほとんど'
    / 'もともと'
    / 'もっと' / 'かなり' / '必ず' / 'できる' ('だけ'/'かぎり')

Adjective = {
    DADJ ('い' / 'かった' / 'くない' / 'な')
    #Adjective
}

Prefix = {
    PREFIX
    #Prefix
}

AdjectivePhrase = {
    Adverb*
    Prefix?
    Adjective
    Prefix?
    #AdjectivePhrase
}

// 動詞 語幹

KA5 = [か書描お置つ付ゆな泣鳴す好ぬ抜き聞聴効続吐履如招引届焼基嘆欺欺背叛眩着突就湧沸輝乾頂戴抜逝欠掻働開閃呟裁] // く
    / '基づ' / 'いただ' / '変わりゆ' / 'つぶや' / 'ひらめ' / 'ごとく'
    / '選り抜' / '据え' [置お] / '見抜' / '差し引' / '置い' [抜ぬ] / '行き届'
    // 立ちゆく　根付く 春めく
IKU5 = [行い]   // か+ず こ+う　き+ます って く け+ば
GA5 = [泳注継] // ぐ
    / '乗り継'
SA5 = [探示貸課出倒貶渡殺写移映直] // す
    / [表著顕現] 'わ'? / [降下お] 'ろ' / [延伸] 'ば' / 'わた' / '散ら' / '尽く'
    / '悩ま' / '巡ら' / '増や' / '減ら' / '冷ま' / '吹か' / '荒ら' / '寝か' / '落と'
    / '貸し出' / '借り倒' / '思い出' / '思い巡ら' / '出尽く' / '出し尽く' / '吹き出'
    / '叩き直' / '目指'
TA5 = [勝も持た立経以打撃討保経育断建発絶勃放好] // つ
    / '並び立' / '役' 'く'? '立' / '目立' / '秩序立' / '組織立' / '規則立' / '際立' / '順序立'
    / '迎え撃' / '解き放'
NA5 = [死] // ぬ
BA5 = [遊呼及滅結飛叫亡学跳選並喜悦運転] // ぶ
    / '思い'? '浮' 'か'? / '持ち運'
MA5 = [よ読詠含こ混込歪挟挑揉包畳拒包畳悩憎弾緩弛因和飲呑勇噛咬励進拝住企痛産生] // む
    / '憐れ' / '悲し' / '悔や' / '危ぶ'
    / '取り組' / '折りたた' / 'たた'
    / '絞り込' / '読み込' / '書き込' / '持ち込' / '組み込' / '払い込'
RA5 = [あ有な成切取送作] // る
    / [謝誤] 'ま'?
    / ([決定高始集] / 'はじ' / 'まと') め
WA5 = [笑使言い扱思] //う
    / 'あつか'
WAU5 = [問] // 問う わ＋ず お＋う い＋ます う＋て う え＋ば

// 上一段 いる 着る 煮る 見る
A1 = [老] 'い'         // いる
KA1 = [起飽尽熾で] 'き' // きる
    / '出来き'
GA1 = [過] 'ぎ'       // ぎる
SA1 = [察達接] 'し'    // しる
ZA1 = [閉恥] 'じ'      // じる
TA1 = ('おっこち' / [落] 'ち') // ちる
NA1 = [煮似]           // にる
BA1 = [浴こち詫] 'び'   // びる
MA1 = [染試] 'み'       // みる
RA1 = [降足] 'り'       // りる
V1 = A1 / KA1 / GA1 / SA1 / ZA1 / TA1 / NA1 / BA1 / MA1 / RA1

//下一段
A4  = [見言考捉与] 'え' // える
KA4 = [受助] 'け'    // ける
GA4 = [告] 'げ'     // げる
SA4 = [見] 'せ'     // せる
ZA4 = [混] 'ぜ'     // ぜる
TA4 = [捨] 'て'     // てる
DA4 = [茹] 'で'     // でる
NA4 = [尋] 'ね'     // ねる
BA4 = [食] 'べ'     // べる
MA4 = [求決定高始集] 'め'   // める
    / 'はじめ' / 'まとめ'
RA4 = [入] 'れ'     // れる
V4 = A4 / KA4 / GA4 / SA4 / ZA4 / TA4 / DA4 / NA4 / BA4 / MA4 / RA4

VNAI = /* ない, なければならない */
    / N ([しじせぜさ] / 'でき' / '出来')
    / V1 / V4
    / KA5 'か' / IKU5 'か' / GA5 'が' / SA5 'さ' / TA5 'た'
    / NA5 'な' / BA5 'ば' / RA5 'ら' / WA5 'わ' / WAU5 'わ'
    / [い居着に似煮み見]  // 上一段
    / [え得で出ね寝へ経]  // 下一段
    / [来こし] // 変格

NAI = VNAI 'ない'
example NAI 入れない

// カ行五段 書 か+ず こ+う き+ます い+て く け+ば
// カ行五段 行
// ガ行５段 泳ぐ が+ず ご+う ぎ+ます い+で ぐ げ＋ば
// サ行五段 探す さ＋ず そ＋う し＋ます し＋て す せ＋ば
// た行5段  勝つ た＋ず と＋う ち＋ます っ＋て　つ てば
// な行五段 死ぬ
// ば行五段
// マ行五段
// ラ行５段 切る ら＋ず ろ＋う り＋ます っ＋て る　れ　切れ
// 　　　　 わたる　渡らず 渡ろう 渡ります 渡って 渡れ　渡れ
// ワ行５段　笑う わ＋ず, お+う, い+ます っ＋て う え＋ば

VTE = /* して, した */
    / N ([しじせぜ] / 'でき' / '出来')
    / V1 / V4
    / KA5 'い' / IKU5 'っ' / GA5 'い' / SA5 'し' / TA5 'っ'
    / NA5 'ん' / BA5 'ん' / RA5 'っ' / WA5 'っ' / WAU5 'う'
    / [い居着に似煮み見]  // 上一段
    / [え得で出ね寝へ経]  // 下一段
    / [来きし] // 変格

VMASU = /* し＋ます */
    / N ([しじせぜ] / 'でき' / '出来')
    / V1 / V4
    / KA5 'き' / IKU5 'き' / GA5 'ぎ' / SA5 'し' / TA5 'ち'
    / NA5 'に' / BA5 'び' / RA5 'り' / WA5 'い' / WAU5 'い'
    / [い居着に似煮み見]  // 上一段
    / [え得で出ね寝へ経]  // 下一段
    / [来きし] // 変格

VEOS = /* する */
    / N ([すず] (&'べき' / 'る') / 'できる' / '出来る')
    / V1 'る' / V4 'る'
    / KA5 'く' / IKU5 'く' / GA5 'ぐ' / SA5 'す' / TA5 'つ'
    / NA5 'ぬ' / BA5 'ぶ' / RA5 'る' / WA5 'う' / WAU5 'う'
    / [い居着に似煮み見] 'る' // 上一段
    / [え得で出ね寝へ経] 'る' // 下一段
    / [来くす] 'る' // 変格

VBA = /* すれ */
    / N ([すず] 'れ' / 'できれ' / '出来れ')
    / V1 'れ' / V4 'れ'
    / KA5 'け' / IKU5 'け' / GA5 'げ' / SA5 'せ' / TA5 'て'
    / NA5 'ね' / BA5 'べ' / RA5 'れ' / WA5 'え' / WAU5 'え'
    / [い居着に似煮み見] 'れ' // 上一段
    / [え得で出ね寝へ経] 'れ' // 下一段
    / [来くす] 'れ' // 変格

VYO = /* しろ */
    / N ([しじ] 'ろ' / [せぜ] 'よ' / 'でき' [ろよ] / '出来' [ろよ])
    / V1 [ろよ] / V4 [えよ]
    / KA5 'け' / IKU5 'け' / GA5 'げ' / SA5 'せ' / TA5 'て'
    / NA5 'ね' / BA5 'べ' / RA5 'れ' / WA5 'え' / WAU5 'え'
    / [い居着に似煮み見] [ろよ] // 上一段
    / [え得で出ね寝へ経] [えよ] // 下一段
    / [来こ] 'い' / 'しろ' / 'せよ' // 変格

example VYO 来い

// Nする 議論する 論じる

NVERB =  /* 未然形 [ない] */ /* なければならない*/
    / N [しじせぜ]
    / 'し'
    / 'ありえ' / 'い'
    / 'なら'
    / 'え' / '得'
    / 'いえ' / '言え' / 'しら' / '知ら'
    / '呼ば' / '含ま' / '示めさ' / [表著顕現] 'わ'? 'さ'
    / '変わら'
    / '使わ' / '用い' / '述べ'
    / 'でき' / '出来' / '助け'
    / ([決定高始集] / 'はじ' / 'まと') 'まら' / ([決定高始集] / 'はじ' / 'まと') 'め'
    / '呼ば' / '含ま' / '使わ' / 'もた' / '持た' / 'あつかわ' / '扱わ'
    / '変わる' / '用い' / '述べ' / 'まとめ' / '異なら' / '集め'
    / '考える' / '試み' / '捉え' / '思わ' / '求め' / '与え'
    / '取ら' / '残ら' / '送ら' / '作ら' / '消さ' / [誤謝] 'ま'? 'ら'
    / '読ま' / '書か' / '防が' / '基づか' / '除か'
    / '始まら' / '始め' / '終わ' / '終え'
    / '増やさ' / '減ら' / '増え' / '減ら' / '加え'


VVERB = /* 過去形 [た], 連用形 [て] */
    / N [しじ]
    / 'し'
    / 'なっ' / 'い'
    / 'あっ' / 'なかっ' / 'なく'
    / 'え' / '得'
    / 'いっ' / '言っ' / 'しっ' / '知っ' / '高まっ'
    / '呼ん' / '含ん' / '示し' / [表著顕現] 'わ'? 'し'
    / '変わっ'
    / '使っ' / '用い' / '述べ'

VERB = /* 基本形 */
    / N [すず] 'る'
    / 'する' / '行う'
    / 'なる'
    / 'ある' / 'いる' / 'ない'
    / 'うる' / '得る' / 'しる' / '知る' / '高まる'
    / 'いう' / '言う' / '示す' / [表著顕現] 'わ'? 'す'
    / 'できる' / '出来る' / '助ける' / '決まる' / '定まる'
    / '呼ぶ' / '含む' / '使う' / 'もつ' / '持つ' / 'あつかう' / '扱う'
    / '変わる' / '用いる' / '述べる' / 'まとめる' / '異なる' / '集める'
    / '考える' / '試みる' / '捉える' / '思う' / '求める' / '与える'
    / '取る' / '残る' / '送る' / '作る' / '消す' / [誤謝] 'ま'? 'る'
    / '読む' / '書く' / '防ぐ' / '基づく' / '除く'
    / '始まる' / '始める' / '終わる' / '終える'
    / '増やす' / '減らす' / '増える' / '減る' / '加える'

CVERB = /* 連用形 [ます] */
    / N [しじ]
    / 'し'
    / 'あり' / 'い'
    / 'なり'
    / 'え' / '得'
    / 'いい' / '言い' / 'しり' / '知り' / '高まり'
    / '呼び' / '含み' / '示し' / [表著顕現] 'わ'? 'せ'
    / '変り'
    / '使い' / '用い' / '述べ'

IVERB = /* 仮定形 [ば] */
    / N [すず] 'れ'
    / 'すれ'
    / 'あれ' / 'いれ' / 'なけれ'
    / 'なら'
    / 'えれ' / '得れ' / 'しれ' / '知れ'
    / 'いえ' / '言え' / '示せ' / [表著顕現] 'わ'? 'せ'
    / '呼べ' / '含め'
    / '変れ'
    / '使え' / '用いれ' / '述べれ'

Verb  = { VERB #Verb }
NVerb = { NVERB #Verb }
CVerb = { CVERB #Verb }
VVerb = { VVERB #Verb }
IVerb = { IVERB #Verb }

"ある"   = 'ある'/'ない'
"るない" = 'る'/'ない'/'た'/'なかった'

VerbPhrase = {
    (Adverb Argument*)* (
        Verb ({'必要が' "ある" #Must} / {'べき' ('で' "ある")? #Should} )?
    / CVerb ({'ます'/'ません' #Do} / {TEN #AndThen})?
    / VVerb ({[ただ] #Did} / {'てい' "るない" #Do} / {'てもよい' #May})
    / NVerb ({'られ' "るない" #BeDone } / { 'な' ('い'/'かった') #Not } / {'なければならない' #Must} )
    / IVerb {'られ' "るない" #Can }
    )
    #VerbPhrase
}

"ので" = 'ため' / 'ので' / 'から'

Connective = {
    ( Adverb Argument* )* (
       VVerb {'て' #With}
    /  IVerb {('ば' / 'れば') #If}
    /  CVerb { #AndThen }
    )
    #ConnectivePhrase
}

Adverb =
    { (DADJ 'く' / DADVERB) #Adverb }

Declinable =
    (VerbPhrase / AdjectivePhrase) (^{'と' (VerbPhrase / AdjectivePhrase) })*

example Noun 友達になります。
example Sentence なります。
example Sentence 素敵な友達になります。
example Sentence 友達の彼女は、「困り者」である
example Sentence 三極一の果報者とする
example Sentence 人生は山あり、谷あり
example Sentence ひらがなを含まない名詞がある

example Sentence ご住職を援助する
example Sentence 道具を使って強く早く攻撃する
example Sentence 「すもも」も「もも」ももものうち
example Sentence 周期的なカタストロフィーの中から不意によく発生する
example Sentence 春とはたとえば無秩序な現象の総称
example Sentence カタストロフィーとは周期的な「秩序だった現象」の中から不意に発生する無秩序な現象の総称
example Sentence 友を知り、友をえる

example Sentence カタストロフィーとは周期的な「秩序だった現象」の中から不意に発生する
example Sentence 友を知り、友をえる
example Sentence 衝突すれば困る

example Sentence 意図しない誤動作

//興味深い

